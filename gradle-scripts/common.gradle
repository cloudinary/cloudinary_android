/**
 * Shared script for every root project, including the topmost root.
 */
allprojects {
    apply plugin: 'maven'
    apply plugin: 'signing'

    buildscript {
        repositories {
            google()
            jcenter()
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:3.2.1'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        maven {
            url "https://maven.google.com"
        }

        jcenter()
        mavenCentral()
    }

    project.ext.set("publishGroupId", group)
}

ext.loadExtraProperties = { fileName ->
    def props = new Properties()
    props.load(new FileInputStream(fileName))

    props.each { key, val ->
        project.ext[key] = val
    }
}

ext.getCloudinaryUrl = { ->
    return findProperty("cloudinary.url") ?: System.getenv('CLOUDINARY_URL')
}

ext.generateAndLoadLocalGradleProperties = { rootPath ->
    // Create default local gradle properties (ignored by git)
    def file = new File(rootPath + 'gradle-local.properties')
    if (file.createNewFile()) {
        file.text = '# Fill in your cloudinary url from cloudinary.com/console.  *** IMPORTANT - This should be the url WITHOUT the api secret.\ncloudinary.url=fill_in_your_cloudinary_url'
    }
    loadExtraProperties file.absoluteFile
}
subprojects {
    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors()
        testLogging.showStandardStreams = true
    }
}